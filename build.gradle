allprojects {
    group = 'org.jdw'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    task wrapper(type: Wrapper) {
        gradleVersion = '2.3'
    }
}

description = 'AMQP Blog'

buildscript {
    ext {
        springBootVersion = '1.3.5.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
        classpath("io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse-wtp'
    apply plugin: 'spring-boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply from: rootProject.file('gradle/eclipse.gradle');

    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        
        // Start: JUnit
        testCompile('org.springframework.boot:spring-boot-starter-test') {
            exclude module: 'commons-logging'
        }
        // End: JUnit
        
        // Start: Spring Boot Web
        compile('org.springframework.boot:spring-boot-starter-web') {
            exclude module: 'spring-boot-starter-tomcat'
        }
        // End: Spring Boot Web
        
        // Start: Database
        compile 'org.springframework.boot:spring-boot-starter-data-jpa'
        compile 'org.hibernate:hibernate-validator:5.1.3.Final' //JSR303 Validation Support
        testCompile 'com.h2database:h2:1.4.185'
        // End: Database
        
        // Start: Apache Commons
        compile 'org.apache.commons:commons-lang3:3.4'
        // End: Apache Commons
        
        // Start: Spring Boot Actuator
        compile 'org.springframework.boot:spring-boot-starter-actuator'
        // End: Spring Boot Actuator

        // Start: Swagger
        compile group: 'io.springfox', name: 'springfox-swagger1', version: '2.5.0'
        compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.5.0'
        // End: Swagger

        // Start: Spring Security
        compile 'org.springframework.boot:spring-boot-starter-security'
        // End: Spring Security
        
        // Start: Hystrix (Circuit breaker)
        compile group: 'com.netflix.hystrix', name: 'hystrix-core', version: '1.5.5'
        compile group: 'com.netflix.hystrix', name: 'hystrix-javanica', version: '1.5.5'
        
        // End: Hystrix (Circuit breaker)

        // Start: Sleuth (Logging correlrelation)
        // https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-starter-sleuth
        compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-sleuth', version: '1.0.4.RELEASE'
        // End: Sleuth (Logging correlation)

        // Start: AMQP
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-amqp'
        // End: AMQP
    }
    
    bootRun {
        // Ensures IntelliJ can load resource files
        addResources = false
    }
}

configure(subprojects.findAll {
        it.name != 'amqp-blog-common'
    }) {
   
    dependencies {
        compile project(':amqp-blog-common')
        testCompile project(path: ':amqp-blog-common', configuration: 'commonTestFiles')
    }
}
